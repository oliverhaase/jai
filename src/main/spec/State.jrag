
aspect State {

	syn State Method.createInitialState() =
		new RegularState(createInitialLocalVars(), createInitialOpStack(),
			createInitialFieldEdges());


	// set of incoming states, statesIn() 	
		
	coll Set<State> Instruction.statesIn() circular [new HashSet<State>()] with addAll root Method;
	Instruction contributes statesOut() to Instruction.statesIn() for each succ();

	syn Set<State> EntryPoint.statesIn() = Collections.singleton(method().createInitialState());
	
		
	// set of outgoing states, statesOut()
	
	syn Set<State> Instruction.statesOut() circular [new HashSet<State>()]; 
      	
    eq Instruction.statesOut() {
    	Set<State> resultSet = new HashSet<State>();

		for ( State stateIn : statesIn() ) {
		   if (stateIn instanceof ExceptionState)
		      resultSet.add(stateIn);
	       else
		      resultSet.add(transferState(((RegularState) stateIn))); 
	    }
		   
		return resultSet;   
	}	
	
    eq LateBoundInvokeInstruction.statesOut() {
        Set<State> resultSet = new HashSet<State>();

        for ( State stateIn : statesIn() ) {
           if (stateIn instanceof ExceptionState)
              resultSet.add(stateIn);
           else
              resultSet.addAll(transferStateOfLateBoundInvokeInstrutction(((RegularState) stateIn))); 
        }
           
        return resultSet;   
    }   
				
	
    //	eq ReturnInstruction.statesOut() = null;
    //	eq Areturn.statesOut() = null;
	
	
	// transfer attribute
	
	syn State Instruction.transferState(RegularState stateIn) =
		new RegularState(transferLocalVars(stateIn.getLocalVars()),
				transferOpStack(stateIn.getOpStack()),
				transferFieldEdges(stateIn.getFieldEdges())); 	
		
	eq StoreInstruction.transferState(RegularState stateIn) =
		new RegularState(transferLocalVars(stateIn.getLocalVars(), stateIn.getOpStack().top()),
				transferOpStack(stateIn.getOpStack()),
				transferFieldEdges(stateIn.getFieldEdges()));
	
	eq LoadInstruction.transferState(RegularState stateIn) =
		new RegularState(transferLocalVars(stateIn.getLocalVars()),
				transferOpStack(stateIn.getOpStack(), stateIn.getLocalVars().get(getIndex())),
				transferFieldEdges(stateIn.getFieldEdges()));
	
	eq FieldAssignmentInstruction.transferState(RegularState stateIn) {
    	// objectref.fieldName = top(opstack)
 
	    ReferenceSlot objectRef = getStoreObjectRef(stateIn.getOpStack());
	    
        if ( objectRef.isNullReference() )
            return new ExceptionState(stateIn.getFieldEdges()); // illegal State (NullPointerException)
        
        if ( getFieldType() instanceof PrimitiveType ) 
			return new RegularState(
				stateIn.getLocalVars(),
                transferOpStack(stateIn.getOpStack()),
				stateIn.getFieldEdges());
					
        return new RegularState(stateIn.getLocalVars(),
				transferOpStack(stateIn.getOpStack()),
                transferFieldEdges(stateIn.getFieldEdges(), objectRef, 
                                   (ReferenceSlot) stateIn.getOpStack().top()));	
    }
    
    syn ReferenceSlot FieldAssignmentInstruction.getStoreObjectRef(OpStack opStack) = null;     
    syn ReferenceSlot PutField.getStoreObjectRef(OpStack opStack) =  (ReferenceSlot) opStack.pop().top();
    syn ReferenceSlot Aastore.getStoreObjectRef(OpStack opStack) =   (ReferenceSlot) opStack.pop().pop().top();
    syn ReferenceSlot PutStatic.getStoreObjectRef(OpStack opStack) = new ReferenceSlot(getGlobalObject());
    
    eq FieldLoadInstruction.transferState(RegularState stateIn) {
    	// push objectref.fieldName onto opstack
    
    	ReferenceSlot objectRef = getLoadObjectRef(stateIn.getOpStack());

		if ( objectRef.isNullReference() )
			return new ExceptionState(stateIn.getFieldEdges()); // illegal State (NullPointerException)
       
		if ( getFieldType() instanceof PrimitiveType ) 
			return new RegularState(
				stateIn.getLocalVars(),
                transferOpStack(stateIn.getOpStack(), PrimitiveSlot.getInstance()),
				stateIn.getFieldEdges());
            
       	Set<ObjectNode> resultObjects = new HashSet<ObjectNode>();
       	Set<FieldEdge> newEdges = new HashSet<FieldEdge>();
	        
	    for ( ObjectNode objectNode : objectRef ) {
			if ( objectNode instanceof GlobalObject ) 
				resultObjects.add(objectNode);				

			if ( objectNode instanceof RealObject ) 
				resultObjects.addAll(stateIn.getFieldEdges().getObjectsOfField(objectNode, getFieldName()));
				
			if ( objectNode instanceof PhantomObject ) {
				resultObjects.addAll(stateIn.getFieldEdges().getObjectsOfField(objectNode, getFieldName()));
				resultObjects.add(this);
				newEdges.add(new FieldEdge(objectNode, getFieldName(), this));
			}				
		}
	                               
        return new RegularState(transferLocalVars(stateIn.getLocalVars()),
                transferOpStack(stateIn.getOpStack(), new ReferenceSlot(resultObjects)),
                transferFieldEdges(stateIn.getFieldEdges(), newEdges));
    }
    
    
    syn ReferenceSlot FieldLoadInstruction.getLoadObjectRef(OpStack opStack) = null;
    syn ReferenceSlot GetField.getLoadObjectRef(OpStack opStack) =  (ReferenceSlot) opStack.top();
    syn ReferenceSlot Aaload.getLoadObjectRef(OpStack opStack) =    (ReferenceSlot) opStack.pop().top();      
    syn ReferenceSlot GetStatic.getLoadObjectRef(OpStack opStack) = new ReferenceSlot(getGlobalObject());
    
    
    eq EarlyBoundInvokeInstruction.transferState(RegularState stateIn) {
        Method targetMethod = getTargetMethod();
        MethodSummary methodSummary = targetMethod.getMethodSummary();
        Map<PhantomObject, Set<ObjectNode>> mapping = mapArguments(stateIn.getOpStack(),
                stateIn.getFieldEdges(), targetMethod.getParams(), methodSummary.getFieldEdges());

        return new RegularState(transferLocalVars(stateIn.getLocalVars()), 
                                transferOpStack(stateIn.getOpStack(), methodSummary.getReturnValue()),
                                transferFieldEdges(stateIn.getFieldEdges(), methodSummary.getFieldEdges(), mapping));
    }

    
    syn Set<State> LateBoundInvokeInstruction.transferStateOfLateBoundInvokeInstrutction(RegularState stateIn) {
        Set<State> resultSet = new HashSet<State>();
        ReferenceSlot objectRef = (ReferenceSlot) stateIn.getOpStack().pop(getConsumeStack() - 1).top();

        if (objectRef.isNullReference())
              return Collections.singleton(new ExceptionState(stateIn.getFieldEdges())); // illegal State (NullPointerException)

        for (Method targetMethod : getTargetMethods(objectRef)) {
            MethodSummary methodSummary = targetMethod.getMethodSummary();
            Map<PhantomObject, Set<ObjectNode>> mapping = mapArguments(stateIn.getOpStack(),
                    stateIn.getFieldEdges(), targetMethod.getParams(), methodSummary.getFieldEdges());

            resultSet.add(new RegularState(transferLocalVars(stateIn.getLocalVars()),
                                           transferOpStack(stateIn.getOpStack(), methodSummary.getReturnValue()),
                                           transferFieldEdges(stateIn.getFieldEdges(), methodSummary.getFieldEdges(), mapping)));
        }
        
        return resultSet;
    }
    
    /**
     * Maps the actual arguments to the formal parameters of the targetMethod.
     * 
     * @param stack
     *            Used to get the arguments for the PhantomObjects
     * @param fieldEdges
     *            Used to get the arguments for the SubPhantomObjects
     * @param params
     *            Used to get the PhantomObjects
     * @param methodSummary
     *            Used to get the SubPhantomObjects
     * @return The mapping from arguments to parameters Map<parameter, Set<argument>>
     */
    protected Map<PhantomObject, Set<ObjectNode>> InvokeInstruction.mapArguments(OpStack stack,
            FieldEdges fieldEdges, List<Type> params, FieldEdges methodSummary) {
        Map<PhantomObject, Set<ObjectNode>> mapping = new HashMap<PhantomObject, Set<ObjectNode>>();
        for (int i = 0; i < params.getNumChild(); i++) {
            Type param = params.getChild(params.getNumChild() - 1 - i);
            if (param instanceof PhantomObject) {
                ReferenceSlot argument = (ReferenceSlot) stack.pop(i).top();
                Set<ObjectNode> arguments = ((ReferenceSlot) argument).getObjects();
                mapping.put((PhantomObject) param, arguments);
                mapSubPhantomObjects(mapping, fieldEdges, methodSummary, (PhantomObject) param);
            }
        }
        return mapping;
    }

    private void InvokeInstruction.mapSubPhantomObjects(Map<PhantomObject, Set<ObjectNode>> mapping,
            FieldEdges fieldEdges, FieldEdges methodSummary, PhantomObject param) {
        for (FieldEdge edge : methodSummary.getEdgesStartingFrom(param)) {
            if (edge.getDestination() instanceof SubPhantomObject) {
                SubPhantomObject subPhantom = (SubPhantomObject) edge.getDestination();
                if (!mapping.containsKey(subPhantom)) {
                    Set<ObjectNode> subArguments = new HashSet<ObjectNode>();
                    for (ObjectNode arg : mapping.get(param)) {
                        subArguments.addAll(fieldEdges.getObjectsOfField(arg, edge.getName()));
                    }
                    mapping.put(subPhantom, subArguments);
                    mapSubPhantomObjects(mapping, fieldEdges, methodSummary, subPhantom);
                }
            }
        }
    }
    
}