aspect MethodSummary {

    syn MethodSummary Method.getMethodSummary() circular [initialMethodSummary()]; 

    eq Method.getMethodSummary() = ((ExitPoint) getInstruction(numChildren - 1)).getMethodSummary();

    syn MethodSummary ExitPoint.getMethodSummary() {
        Set<FieldEdge> fieldEdges = new HashSet<FieldEdge>();
        Set<ObjectNode> objectNodes = new HashSet<ObjectNode>();
        for (State state : statesIn()) {
            if (state instanceof RegularState) {
                fieldEdges.addAll(((RegularState) state).getFieldEdges().getEdges());
                if (method().getReturnType() instanceof ReferenceType)
                    objectNodes.addAll(((ReferenceSlot) ((RegularState) state).getOpStack().top()).getObjects());
            }
        }
        return new MethodSummary(new FieldEdges(fieldEdges),
                                 method().getReturnType().getSlot(new ReferenceSlot(objectNodes)));
    }
        
    eq NativeMethod.getMethodSummary() {
        Set<FieldEdge> fieldEdges = new HashSet<FieldEdge>();
        for (Type param : getParams()) {
            if (param instanceof ReferenceType)
                fieldEdges.add(new FieldEdge(getGlobalObject(), "initalMethodSummary", (ObjectNode) param));
        }
        return new MethodSummary(new FieldEdges(fieldEdges),
                                 getReturnType().getSlot(new ReferenceSlot(getGlobalObject())));
    }
    
    syn MethodSummary Method.initialMethodSummary() = 
            new MethodSummary(FieldEdges.getInstance(), getReturnType().getInitialSlot());
    
    syn Slot TypeOrVoid.getSlot(ReferenceSlot refTypeSlot);
    eq Void.getSlot(ReferenceSlot refTypeSlot) = ReferenceSlot.getNullReference();
    eq ReferenceType.getSlot(ReferenceSlot refTypeSlot) = refTypeSlot;
    eq PrimitiveType.getSlot(ReferenceSlot refTypeSlot) = PrimitiveSlot.getInstance();
    
    syn Slot TypeOrVoid.getInitialSlot();
    eq Void.getInitialSlot() = ReferenceSlot.getNullReference();
    eq ReferenceType.getInitialSlot() = new ReferenceSlot((ObjectNode) this);
    eq PrimitiveType.getInitialSlot() = PrimitiveSlot.getInstance();
}